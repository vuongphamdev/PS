buildscript {
	repositories {
		mavenCentral()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
	dependencies {
		classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.14.3'
		classpath 'com.pswidersk:python-gradle-plugin:2.8.1'
	}
}

plugins {
	id 'distribution'
}



apply plugin: 'com.pswidersk.python-plugin'
apply plugin: 'com.diffplug.spotless'

pythonPlugin {
	pythonVersion = "3.11.5"
}

ext {
	pythonWorkDir = layout.buildDirectory.dir('python/working').get()
}

tasks.register('pipInstall', com.pswidersk.gradle.python.VenvTask) {
	Directory distDir = layout.projectDirectory.dir('src/main/dist')

	inputs.files(distDir.asFileTree)
	outputs.files(pythonWorkDir.asFileTree)
	workingDir.set(distDir)
	venvExec = 'pip'
	args = [
		'install',
		'-r',
		'requirements.txt',
		'-t',
		pythonWorkDir
	]
}

distributions {
	main {
		contents {
			from pythonWorkDir
			exclude('**/__pycache__', 'venv')
			into '/'
		}
	}
}

tasks.named('distZip').configure {
	archiveFileName.set("custom-name.zip")
	dependsOn('pipInstall')
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}
tasks.named('distTar').configure { dependsOn('pipInstall') }
tasks.named('installDist').configure { dependsOn('pipInstall') }
